! Copyright © 2015–2023 Alberta Language Technology Lab (ALTLab)
! http://altlab.ualberta.ca/
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU Affero General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU Affero General Public License for more details.
!
! You should have received a copy of the GNU Affero General Public License
! along with this program. If not, see <https://www.gnu.org/licenses/>.

! ========================================================================== !
!! # Plains Cree numerals                           
! ========================================================================== !

!! ## The file for numerals

LEXICON Numerals !!= * @CODE@
pîyak% kihci% mitâtahtomtanaw THOUSAND ;
pîyak% kihci% mitâtahtomitanaw THOUSAND ;
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
ONES ;			! 1-9
NEARLY ;			! 29-99
ARABIC          ; ! for the arabic numerals !
ROMAN           ; ! for the roman numerals  !
NUM-PREFIXES    ; ! for §34 etc.            !
ISOLATED-NUMEXP ; ! for ½ etc.                      !



LEXICON THOUSAND
% athiwâk%  UNDERTHOUSAND ;	   ! 1100-1999
% athiwâk%  TENS ;	   ! 1020-1099
% athiwâk%  TEENS ;	   ! 1010-1019
% athiwâk%  ONES ;	   ! 1001-1009
 NUM ;	   ! 1000


!===========================
!! ## Here start the 999 numbers
!===========================

LEXICON UNDERTHOUSAND !!= * @CODE@
HUNDREDS ;
mitâtahtomitanaw  HUNDRED ;
mitâtahtomtanaw  HUNDRED ;

LEXICON HUNDREDS !!= * @CODE@
nîso%    CUODI ;
nisto%     CUODI ;
nîwo%  CUODI ;
niyânano%    CUODI ;  !OBS
nikotwâsiko%    CUODI ;
tîpakohpo%  CUODI ;
ayinânîwo%  CUODI ;
kîkâmitâtahto%    CUODI ;

LEXICON CUODI !!= * @CODE@
mitâtahtomitanaw    HUNDRED ;
mitâtahtomtanaw   HUNDRED ;
 
LEXICON HUNDRED !!= * @CODE@
% athiwâk%   TENS ;
% athiwâk%   TEENS ;
% athiwâk%    ONES ;
 NUM ;



LEXICON TENS !!= * @CODE@
mitâtaht NUM ;
nîs TEN ;
nîsomi TEN;
nîsi TEN ;
nistomi TEN ;
nîmi TEN ;
niyânanomi TEN ;
nikotwâsomi TEN ;
tîpakohpimi TEN ;
tîpakohpomi TEN ;
ayinânîwomi TEN ;
kîkâ-mitâtahtomi TEN ;
kîkâmitâtahtomi TEN ;

LEXICON TEN !!= * @CODE@
tanaw%  ONESTONEXT ;
tanaw  NUM ;

LEXICON NEARLY
kîkâc% nistom NEARLY-TEN ;
kîkâc% nistomi NEARLY-TEN ;
kîkâc% nîwom NEARLY-TEN ;
kîkâc% nîmi NEARLY-TEN ;
kîkâc% nîwomi NEARLY-TEN ;
kîkâc% nîyânom NEARLY-TEN ;
kîkâc% nîyânomi NEARLY-TEN ;
kîkâc% nikotwâsom NEARLY-TEN ;
kîkâc% tîpakohpim NEARLY-TEN ;
kîkâc% ayinânîwom NEARLY-TEN ;
kîkâc% kîkâ% mitâtahtomi NEARLY-TEN ;
kîkâc% kîkâ% mitâtahtom NEARLY-TEN ;
kîkâ% mitâtahtom NEARLY-TEN ;
kîkâ% mitâtahtomi NEARLY-TEN ;
kîkâ% mitatahtomitanaw% athiwâk% kîkâ% mitâtaht NUM ;
kîkâc-nistomi NEARLY-TEN ;
kîkâ-nistomi NEARLY-TEN ;
kîkâc-nîmi NEARLY-TEN ;
kîkâc-nîwomi NEARLY-TEN ;
kîkâ-nîmi NEARLY-TEN ;
kîkâc-niyânanomi NEARLY-TEN ;
kîkâ-niyânanomi NEARLY-TEN ;
kîkâc-nikotwâsomi NEARLY-TEN ;
kîkâ-nikotwâsomi NEARLY-TEN ;
kîkâc-tîpakohpimi NEARLY-TEN ;
kîkâc-tîpakohpomi NEARLY-TEN ;
kîkâ-tîpakohpimi NEARLY-TEN ;
kîkâ-tîpakohpomi NEARLY-TEN ;
kîkâc-ayinânîwomi NEARLY-TEN ;
kîkâ-ayinânîwomi NEARLY-TEN ;
kîkâc-kîkâ-mitâtahtomi NEARLY-TEN ;
kîkâ-mitâtahtomi NEARLY-TEN ;
kîkâ-mitâtahtomitanaw% kîkâ-mitâtaht NUM ;

LEXICON NEARLY-TEN
tanaw NUM ;


LEXICON ONESTONEXT !!= * @CODE@
pîyakosâp NUM ;
nîsosâp NUM ;
nistosâp NUM ;
nîwosâp NUM ;
niyânanosâp NUM ;
nikotwâsosâp NUM ;
tîpakohposâp NUM ;
ayinânîwosâp NUM ;
kîkâ-mitâtahtosâp NUM ;
kîkâmitâtahtosâp NUM ;
pîyako-sâp NUM ;
nîso-sâp NUM ;
nisto-sâp NUM ;
nîwo-sâp NUM ;
niyânano-sâp NUM ;
nikotwâso-sâp NUM ;
tîpakohp-sâp NUM ;
ayinânîwo-sâp NUM ;
!kîkâc% nîsitanaw NUM ; ! only for 19 (almost 20)
athiwâk%  CARDINAL ;



LEXICON TEENS !!= * @CODE@
pîyakosâp NUM ;
nîsosâp NUM ;
nistosâp NUM ;
nîwosâp NUM ;
niyânanosâp NUM ;
nikotwâsosâp NUM ;
nikotwâsikosâp NUM ;
tîpakohposâp NUM ;
ayinânîwosâp NUM ;
kîkâ-mitâtahtosâp NUM ;
kîkâmitâtahtosâp NUM ;
kîkâc% nîsitanaw NUM ;
kîkâ-nîsitanaw NUM ;

LEXICON ONES !!= * @CODE@
CARDINAL ;
!ORDINAL ;

LEXICON CARDINAL !!= * @CODE@
pîyak NUM ;
nîso NUM ;
nisto NUM ;
nîwo NUM ;
niyânan NUM ;
nikotwâsik NUM ;
tîpakohp NUM ;
ayinânîw NUM ;
kîkâ-mitâtaht NUM ;
kîkâmitâtaht NUM ;
kîkâ% mitâtaht NUM ;

LEXICON NUM !!= * @CODE@ adds Ipc+Num
+Ipc+Num: # ;
