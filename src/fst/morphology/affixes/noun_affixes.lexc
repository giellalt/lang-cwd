! Copyright © 2015–2024 Alberta Language Technology Lab (ALTLab)
! http://altlab.ualberta.ca/
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU Affero General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU Affero General Public License for more details.
!
! You should have received a copy of the GNU Affero General Public License
! along with this program. If not, see <https://www.gnu.org/licenses/>.

! noun_affixes.lexc
! ! ALL OF AEW NI CLASSES GO TO A SINGLE NI LEXICON
! ! ALL OF AEW NA CLASSES GO TO A SINGLE NA LEXICON
! ! ALL OF AEW DEPENDENT NA CLASSES GO TO A SINGLE NAD LEXICON (including kinship)
! ! ALL OF AEW DEPENDENT NI CLASSES GO TO A SINGLE NID LEXICON
! ! i2, i4 (animate dependent only), w3 (animate dependnet nouns only) should be the only special character we need for the noun affixes; -im possesion is done by flag diacritics in the affixes file

! Noun affixes

LEXICON NOUN_PREFIXES
@P.noun.Indep@     NOUN_INDEP_PREFIXES ;
@P.noun.DepKin@    NOUN_DEP_KINSHIP_PREFIXES ;
@P.noun.DepNonKin@ NOUN_DEP_NONKINSHIP_PREFIXES ;

LEXICON NOUN_INDEP_PREFIXES
@P.person.NI@:@P.person.NI@nit2%<  PRENOUNS_GATE ;
@P.person.KI@:@P.person.KI@kit2%<  PRENOUNS_GATE ;
@P.person.O@:@P.person.O@ot2%<     PRENOUNS_GATE ;
@P.person.NULL@                    PRENOUNS_GATE ;

LEXICON NOUN_DEP_KINSHIP_PREFIXES
@P.person.NI@:@P.person.NI@ni4%<   PRENOUNS_GATE ;
@P.person.KI@:@P.person.KI@ki4%<   PRENOUNS_GATE ;
@P.person.O@:@P.person.O@w3%<      PRENOUNS_GATE ;

LEXICON NOUN_DEP_NONKINSHIP_PREFIXES
@P.person.NI@:@P.person.NI@ni4%<   PRENOUNS_GATE ;
@P.person.KI@:@P.person.KI@ki4%<   PRENOUNS_GATE ;
@P.person.O@:@P.person.O@w3%<      PRENOUNS_GATE ;
@P.person.MI@0:@P.person.MI@0mi4%< PRENOUNS_GATE ;

LEXICON PRENOUNS_GATE
NOUNSTEMS_GATE ;
PRENOUNS ;

!! Here we continue to lexical prenouns (in prenouns.lexc)
!
! LEXICON PRENOUNS
! PN/prenoun+:prenoun PRENOUNS_BOUND ;
! ...
! Here we return from prenouns

LEXICON PRENOUNS_BOUND
@U.joiner.NULL@@D.cnj.CC@:@U.joiner.NULL@@D.cnj.CC@%^EGLOT%< PRENOUNS_GATE ;
@U.joiner.hyphen@@D.cnj.CC@:@U.joiner.hyphen@@D.cnj.CC@-%<   PRENOUNS_GATE ;
! @D.cnj.CC@@P.frag.FRAG@ NOUN_FRAG ;

@U.joiner.NULL@@R.cnj.CC@@C.cnj@:@U.joiner.NULL@@R.cnj.CC@@C.cnj@%^IC%^EGLOT%< PRENOUNS_GATE ;
@U.joiner.hyphen@@R.cnj.CC@@C.cnj@:@U.joiner.hyphen@@R.cnj.CC@@C.cnj@%^IC-%<   PRENOUNS_GATE ;
! @R.cnj.CC@@C.cnj@@P.frag.FRAG@:@R.cnj.CC@@C.cnj@@P.frag.FRAG@%^IC NOUN_FRAG ;

LEXICON NOUNSTEMS_GATE
@R.noun.Indep@     NOUN_INDEP_STEMS ;
@R.noun.DepKin@    NOUN_DEP_KINSHIP_STEMS ;
@R.noun.DepNonKin@ NOUN_DEP_NONKINSHIP_STEMS ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Looping through the NOUN stems in the following LEXICONs
!
! LEXICON NOUN_INDEP_STEMS
! asikan NA ;
!
! LEXICON NOUN_DEP_KINSHIP_STEMS
! nôhkom:ohkom NA ;
!
! LEXICON NOUN_DEP_NONKINSHIP_STEMS
! mitâs:tâs NA ;
!
! End of place-holder noun stems
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! Setting up flag-diacritics based on stem contlexica for possessive -im/0 and diminutive -is/-isis

! NA

LEXICON NA_POSS/IM
@P.theme.IM@ NA ;

LEXICON NA_POSS/IM2
@P.theme.IM2@ NA ;

LEXICON NA_DIM/IS
@P.dim.IS@ NA ;

LEXICON NA_DIM/ISIS
@P.dim.ISIS@ NA ;

LEXICON NA_DIM/DIM
@P.dim.DIM@ NA ;

LEXICON NA_POSS/IM_DIM/IS
@P.theme.IM@ NA_DIM/IS ;

LEXICON NA_POSS/IM_DIM/ISIS
@P.theme.IM@ NA_DIM/ISIS ;

LEXICON NA_POSS/IM2_DIM/IS
@P.theme.IM2@ NA_DIM/IS ;

LEXICON NA_POSS/IM2_DIM/ISIS
@P.theme.IM2@ NA_DIM/ISIS ;

LEXICON NA_SG/A_POSS/IM
@P.sg.A@ NA_POSS/IM ;

LEXICON NA_SG/I_POSS/IM
@P.sg.I@ NA_POSS/IM ;

LEXICON NA_SG/A_POSS/IM_DIM/IS
@P.sg.A@ NA_POSS/IM_DIM/IS ;

LEXICON NA_SG/A_POSS/IM_DIM/ISIS
@P.sg.A@ NA_POSS/IM_DIM/ISIS ;

LEXICON NA_SG/I_POSS/IM_DIM/IS
@P.sg.I@ NA_POSS/IM_DIM/IS ;

LEXICON NA_SG/I_POSS/IM_DIM/ISIS
@P.sg.I@ NA_POSS/IM_DIM/ISIS ;

! NAD

LEXICON NAD_POSS/IM
@P.theme.IM@ NAD ;

LEXICON NAD_POSS/IM2
@P.theme.IM2@ NAD ;

LEXICON NAD_DIM/IS
@P.dim.IS@ NAD ;

LEXICON NAD_DIM/ISIS
@P.dim.ISIS@ NAD ;

LEXICON NAD_DIM/DIM
@P.dim.DIM@ NAD ;

LEXICON NAD_POSS/IM_DIM/IS
@P.theme.IM@ NAD_DIM/IS ;

LEXICON NAD_POSS/IM_DIM/ISIS
@P.theme.IM@ NAD_DIM/ISIS ;

LEXICON NAD_POSS/IM2_DIM/IS
@P.theme.IM2@ NAD_DIM/IS ;

LEXICON NAD_POSS/IM2_DIM/ISIS
@P.theme.IM2@ NAD_DIM/ISIS ;

LEXICON NAD_SG/A
@P.sg.A@ NAD ;

LEXICON NAD_SG/I
@P.sg.I@ NAD ;

LEXICON NAD_SG/A_POSS/IM
@P.sg.A@ NAD_POSS/IM ;

LEXICON NAD_SG/I_POSS/IM
@P.sg.I@ NAD_POSS/IM ;

LEXICON NAD_SG/A_POSS/IM_DIM/IS
@P.sg.A@ NAD_POSS/IM_DIM/IS ;

LEXICON NAD_SG/A_POSS/IM_DIM/ISIS
@P.sg.A@ NAD_POSS/IM_DIM/ISIS ;

LEXICON NAD_SG/I_POSS/IM_DIM/IS
@P.sg.I@ NAD_POSS/IM_DIM/IS ;

LEXICON NAD_SG/I_POSS/IM_DIM/ISIS
@P.sg.I@ NAD_POSS/IM_DIM/ISIS ;

! NI

LEXICON NI_POSS/IM
@P.theme.IM@ NI ;

LEXICON NI_POSS/IM2
@P.theme.IM2@ NI ;

LEXICON NI_DIM/IS
@P.dim.IS@ NI ;

LEXICON NI_DIM/ISIS
@P.dim.ISIS@ NI ;

LEXICON NI_DIM/DIM
@P.dim.DIM@ NI ;

LEXICON NI_POSS/IM_DIM/IS
@P.theme.IM@ NI_DIM/IS ;

LEXICON NI_POSS/IM_DIM/ISIS
@P.theme.IM@ NI_DIM/ISIS ;

LEXICON NI_POSS/IM2_DIM/IS
@P.theme.IM2@ NI_DIM/IS ;

LEXICON NI_POSS/IM2_DIM/ISIS
@P.theme.IM2@ NI_DIM/ISIS ;

LEXICON NI_SG/I_POSS/IM
@P.sg.I@ NI_POSS/IM ;

LEXICON NI_SG/I_POSS/IM_DIM/IS
@P.sg.I@ NI_POSS/IM_DIM/IS ;

LEXICON NI_SG/I_POSS/IM_DIM/ISIS
@P.sg.I@ NI_POSS/IM_DIM/ISIS ;

! NID

LEXICON NID_POSS/IM
@P.theme.IM@ NID ;

LEXICON NID_POSS/IM2
@P.theme.IM2@ NID ;

LEXICON NID_DIM/IS
@P.dim.IS@ NID ;

LEXICON NID_DIM/ISIS
@P.dim.ISIS@ NID ;

LEXICON NID_DIM/DIM
@P.dim.DIM@ NID ;

LEXICON NID_POSS/IM_DIM/IS
@P.theme.IM@ NID_DIM/IS ;

LEXICON NID_POSS/IM_DIM/ISIS
@P.theme.IM@ NID_DIM/ISIS ;

LEXICON NID_POSS/IM2_DIM/IS
@P.theme.IM2@ NID_DIM/IS ;

LEXICON NID_POSS/IM2_DIM/ISIS
@P.theme.IM2@ NID_DIM/ISIS ;

LEXICON NID_SG/I
@P.sg.I@ NID ;

LEXICON NID_SG/I_POSS/IM
@P.sg.I@ NID_POSS/IM ;

LEXICON NID_SG/A_POSS/IM_DIM/ISIS
@P.sg.A@ NID_POSS/IM_DIM/ISIS ;

LEXICON NID_SG/I_POSS/IM_DIM/IS
@P.sg.I@ NID_POSS/IM_DIM/IS ;

LEXICON NID_SG/I_POSS/IM_DIM/ISIS
@P.sg.I@ NID_POSS/IM_DIM/ISIS ;

! #####

! LEXICON NI_POSS/IM
! @P.im.IM@ NI ;
! 
! LEXICON NI_DIM/IS
! @P.is.IS@ NI ;
! 
! LEXICON NI_DIM/ISIS
! @P.isis.ISIS@ NI ;
! 
! LEXICON NI_POSS/IM_DIM/IS
! @P.im.IM@@P.is.IS@ NI ;
! 
! LEXICON NI_POSS/IM_DIM/ISIS
! @P.im.IM@@P.isis.ISIS@ NI ;
! 
! LEXICON NI_SG/I_POSS/IM
! @U.sg.I@ NI_POSS/IM ;
!
! LEXICON NI_SG/I_POSS/IM_DIM/IS
! @U.sg.I@ NI_POSS/IM_DIM/IS ;
!
! NID
!
! LEXICON NID_POSS/IM
! @P.im.IM@ NID ;
!
! LEXICON NID_DIM/IS
! @P.is.IS@ NID ;
!
! LEXICON NID_DIM/ISIS
! @P.isis.ISIS@ NID ;
!
! LEXICON NID_POSS/IM_DIM/IS
! @P.im.IM@@P.is.IS@ NID ;
! 
! LEXICON NID_POSS/IM_DIM/ISIS
! @P.im.IM@@P.isis.ISIS@ NID ;
!
! LEXICON NID_SG/I
! @U.sg.I@ NID ;
! 
! LEXICON NID_SG/I_DIM/IS
! @U.sg.I@ NID_DIM/IS ;

!! Starting with the general morphosyntactic features in a standardized order

LEXICON NA
+N+A:0 NA_POSS_GATE1 ;

LEXICON NAD
@P.dep.DEP@+N+A+D:@P.dep.DEP@0 NA_POSS_GATE1 ;

LEXICON NI
+N+I:0 NI_POSS_GATE1 ;

LEXICON NID
@P.dep.DEP@+N+I+D:@P.dep.DEP@0 NI_POSS_GATE1 ;

! LEXICON NI
! +N+I:0 NI_POSS_IM_0_SUFFIX ;
! 
! LEXICON NID
! @P.dep.DEP@+N+I+D:@P.dep.DEP@0 NI_POSS_IM_0_SUFFIX ;

!! Suffixation for animate nouns

LEXICON NA_POSS_GATE1
@D.person.NULL@ NA_THEME_SUFFIX1 ;
@R.person.NULL@ NA_DIM_GATE ;

LEXICON NA_THEME_SUFFIX1
! Standard forms
@D.theme.IM@0                                                    NA_DIM_GATE ;
@R.theme.IM@@C.sg@:@R.theme.IM@@C.sg@%>i2m                       NA_DIM_GATE ;
@R.theme.IM2@@C.sg@:@R.theme.IM2@@C.sg@%>i2m                     NA_POSS_SUFFIXES ;
! Non-standard forms
@D.theme.IM@@P.err.Thm@@C.sg@:@D.theme.IM@@P.err.Thm@@C.sg@%>i2m NA_DIM_GATE ;
@R.theme.IM@@P.err.Thm@0                                         NA_DIM_GATE ;
@R.theme.IM2@@P.err.Thm@0                                        NA_POSS_SUFFIXES ;

LEXICON NA_DIM_GATE
@P.dim.NULL@                          NA_POSS_GATE2 ;
@D.dep@+Der/Dim+N+A:@D.dep@           NA_DIM_SUFFIX ;
@R.dep.DEP@+Der/Dim+N+A+D:@R.dep.DEP@ NA_DIM_SUFFIX ;

LEXICON NA_DIM_SUFFIX
! Standard forms
@R.dim.IS@@C.sg@:@R.dim.IS@@C.sg@%>i2^DIMs                               NA_POSS_GATE2 ;
@R.dim.ISIS@@C.sg@:@R.dim.ISIS@@C.sg@%>i2^DIMsîs                         NA_POSS_GATE2 ;
@D.dim@@C.sg@:@D.dim@@C.sg@%>i2^DIMs                                     NA_POSS_GATE2 ;
@D.dim@@C.sg@:@D.dim@@C.sg@%>i2^DIMsîs                                   NA_POSS_GATE2 ;
! Non-standard forms
@R.dim.DIM@@P.err2.Dim@@C.sg@:@R.dim.DIM@@P.err2.Dim@@C.sg@%>i2^DIMs     NA_POSS_GATE2 ;
@R.dim.DIM@@P.err2.Dim@@C.sg@:@R.dim.DIM@@P.err2.Dim@@C.sg@%>i2^DIMsîs   NA_POSS_GATE2 ;
@R.dim.ISIS@@P.err2.Dim@@C.sg@:@R.dim.ISIS@@P.err2.Dim@@C.sg@%>i2^DIMs   NA_POSS_GATE2 ;
@R.dim.IS@@P.err2.Dim@@C.sg@:@R.dim.IS@@P.err2.Dim@@C.sg@%>i2^DIMsîs     NA_POSS_GATE2 ;

LEXICON NA_POSS_GATE2
@D.person.NULL@ NA_THEME_SUFFIX2 ;
@R.person.NULL@ NA_NUMBER_OBV_LOC_SUFFIXES ;

LEXICON NA_THEME_SUFFIX2
! Standard forms
@D.theme.IM2@0                                                                             NA_POSS_SUFFIXES ;
@D.dim.NULL@@R.theme.IM2@@C.sg@:@D.dim.NULL@@R.theme.IM2@@C.sg@%>i2m                       NA_POSS_SUFFIXES ;
! Non-standard forms
@D.dim.NULL@@R.theme.IM2@@P.err.Thm@0                                                      NA_POSS_SUFFIXES ;
@D.dim.NULL@@D.theme.IM2@@P.err.Thm@@C.sg@:@D.dim.NULL@@D.theme.IM2@@P.err.Thm@@C.sg@%>i2m NA_POSS_SUFFIXES ;

! LEXICON NA_POSS_THEME_SUFFIX
! @R.person.NULL@                                                                            NA_DIM_SUFFIXES ;
! @D.person.NULL@@R.im.IM@@C.sg@:@D.person.NULL@@R.im.IM@@C.sg@%>i2m                         NA_DIM_SUFFIXES ;
! @D.person.NULL@@R.im.IM2@@C.sg@:@D.person.NULL@@R.im.IM2@@C.sg@%>i2m                       NA_POSS_SUFFIXES ;
! @D.person.NULL@@R.im.IM2@@P.err.Thm@@C.sg@:@D.person.NULL@@R.im.IM2@@P.err.Thm@@C.sg@%>i2m NA_DIM_SUFFIXES ;
! @D.person.NULL@@R.im.IM@@P.err.Thm@:@D.person.NULL@@R.im.IM@@P.err.Thm@0                   NA_DIM_SUFFIXES ; ! Incorrectly using 0-poss rather than im-poss
! @D.person.NULL@@D.im.IM@                                                                   NA_DIM_SUFFIXES ;
! @D.person.NULL@@D.im.IM@@P.err.Thm@@C.sg@:@D.person.NULL@@D.im.IM@@P.err.Thm@@C.sg@%>i2m   NA_DIM_SUFFIXES ; ! Incorrectly using im-poss rather than 0-poss
!
! LEXICON NA_DIM_SUFFIXES
! @D.person.NULL@          NA_POSS_SUFFIXES ;
! @R.person.NULL@ NA_NUMBER_OBV_LOC_SUFFIXES ;
! Independent NAs
! @D.dep.DEP@@D.dim@@D.isis.ISIS@@C.sg@+Der/Dim+N+A:@D.dep.DEP@@D.dim@@D.isis.ISIS@@C.sg@%>i2%^DIMs   NA_POSS_THEME2_SUFFIX ;
! @D.dep.DEP@@D.dim@@D.is.IS@@C.sg@+Der/Dim+N+A:@D.dep.DEP@@D.dim@@D.is.IS@@C.sg@%>i2%^DIMsîs         NA_POSS_THEME2_SUFFIX ;
! @D.dep.DEP@@D.dim@@R.isis.ISIS@@C.sg@@P.err2.Dim@+Der/Dim+N+A:@D.dep.DEP@@D.dim@@R.isis.ISIS@@C.sg@@P.err2.Dim@%>i2%^DIMs   NA_POSS_THEME2_SUFFIX ;
! @D.dep.DEP@@D.dim@@R.is.IS@@C.sg@@P.err2.Dim@+Der/Dim+N+A:@D.dep.DEP@@D.dim@@R.is.IS@@C.sg@@P.err2.Dim@%>i2%^DIMsîs         NA_POSS_THEME2_SUFFIX ;
! Dependent NAs
! @R.dep.DEP@@D.dim@@D.isis.ISIS@@C.sg@+Der/Dim+N+A+D:@R.dep.DEP@@D.dim@@D.isis.ISIS@@C.sg@%>i2%^DIMs NA_POSS_THEME2_SUFFIX ;
! @R.dep.DEP@@D.dim@@D.is.IS@@C.sg@+Der/Dim+N+A+D:@R.dep.DEP@@D.dim@@D.is.IS@@C.sg@%>i2%^DIMsîs       NA_POSS_THEME2_SUFFIX ;
! @R.dep.DEP@@D.dim@@R.isis.ISIS@@C.sg@@P.err2.Dim@+Der/Dim+N+A+D:@R.dep.DEP@@D.dim@@D.isis.ISIS@@C.sg@@P.err2.Dim@%>i2%^DIMs NA_POSS_THEME2_SUFFIX ;
! @R.dep.DEP@@D.dim@@R.is.IS@@C.sg@@P.err2.Dim@+Der/Dim+N+A+D:@R.dep.DEP@@D.dim@@D.is.IS@@C.sg@@P.err2.Dim@%>i2%^DIMsîs       NA_POSS_THEME2_SUFFIX ;
!
! LEXICON NA_POSS_THEME2_SUFFIX
! @R.person.NULL@                                                                            NA_NUMBER_OBV_LOC_SUFFIXES ;
! @D.person.NULL@@D.im@                                                                      NA_POSS_SUFFIXES ;
! @D.person.NULL@@D.im@@P.err.Thm@:@D.person.NULL@@D.im@@P.err.Thm@%>i2m                     NA_POSS_SUFFIXES ;
! @D.person.NULL@@R.im.IM2@@C.sg@:@D.person.NULL@@R.im.IM2@@C.sg@%>i2m                       NA_POSS_SUFFIXES ;
! @D.person.NULL@@R.im.IM2@@P.err.Thm@:@D.person.NULL@@R.im.IM2@@P.err.Thm@0                 NA_POSS_SUFFIXES ;
! @D.person.NULL@@R.im.IM@@P.err.Thm@@C.sg@:@D.person.NULL@@R.im.IM@@P.err.Thm@@C.sg@%>i2m   NA_POSS_SUFFIXES ;
! @D.person.NULL@@R.im.IM@:@D.person.NULL@@R.im.IM@0                                         NA_POSS_SUFFIXES ;

LEXICON NA_POSS_SUFFIXES
! Singular possessors
@U.person.NI@+Px1Sg:@U.person.NI@0 NA_NUMBER_OBV_LOC_SUFFIXES ;
@U.person.KI@+Px2Sg:@U.person.KI@0 NA_NUMBER_OBV_LOC_SUFFIXES ;
@U.person.O@+Px3Sg:@U.person.O@0   NA_NUMBER_OBV_LOC_SUFFIXES ;
@U.person.NULL@                    NA_NUMBER_OBV_LOC_SUFFIXES ;
! Plural possessors
@U.person.NI@@C.sg@+Px1Pl:@U.person.NI@@C.sg@%>i2nân  NA_NUMBER_OBV_LOC_SUFFIXES ;
@U.person.KI@@C.sg@+Px12Pl:@U.person.KI@@C.sg@%>i2naw NA_NUMBER_OBV_LOC_SUFFIXES ;
@U.person.KI@@C.sg@+Px2Pl:@U.person.KI@@C.sg@%>i2wâw  NA_NUMBER_OBV_LOC_SUFFIXES ;
@U.person.O@@C.sg@+Px3Pl:@U.person.O@@C.sg@0%>i2wâw   NA_NUMBER_OBV_LOC_SUFFIXES ;
! Obviative possessor
@U.person.O@@C.sg@+Px4Sg/Pl:@U.person.O@@C.sg@%>i2thiw NA_NUMBER_OBV_LOC_SUFFIXES ;
! Unspeficied possessor
@U.person.MI@+PxX:@U.person.MI@0        NA_NUMBER_OBV_LOC_SUFFIXES ;
@D.loc.NULL@ NA_POSS_LOC_COMBO_SUFFIXES ;

LEXICON NA_POSS_LOC_COMBO_SUFFIXES
@U.person.NI@+Px1Sg+Loc:@U.person.NI@%>i2hk NOUN_ENDLEX ;
@U.person.KI@+Px2Sg+Loc:@U.person.KI@%>i2hk NOUN_ENDLEX ;
@U.person.O@+Px3Sg+Loc:@U.person.O@%>i2hk   NOUN_ENDLEX ;
! Plural possessors
@U.person.NI@+Px1Pl+Loc:@U.person.NI@%>i2nâhk  NOUN_ENDLEX ;
@U.person.KI@+Px12Pl+Loc:@U.person.KI@%>i2nâhk NOUN_ENDLEX ;
@U.person.KI@+Px2Pl+Loc:@U.person.KI@%>i2wâhk  NOUN_ENDLEX ;
@U.person.O@+Px3Pl+Loc:@U.person.O@0%>i2wâhk   NOUN_ENDLEX ;
! Obviative possessor
@U.person.O@+Px4Sg/Pl+Loc:@U.person.O@%>i2thihk NOUN_ENDLEX ;
! Unspecified possessor
@U.person.MI@+PxX+Loc:@U.person.MI@%>i2hk          NOUN_ENDLEX ;
@U.person.MI@+PxX+Distr:@U.person.MI@%>i2nâhk      NOUN_ENDLEX ;

! POSS IM SG   SUFF
! 12   -  A    -a
! 3    -  A    -a
! 12   +  A    -im-0
! 3    +  A    -im-a
! 12   +  0    -im-0
! 3    +  0    -im-a

LEXICON NA_NUMBER_OBV_LOC_SUFFIXES
! Singular
@D.number.PL@@D.person.O@ NA_NUMBER_SUFFIXES_SG ; ! Singular suffix: non-3rd-person possession; non-plural-only lexemes
! Obviative
@R.person.NULL@+Obv:@R.person.NULL@%>a NOUN_ENDLEX ; ! non-possessed, obviative -a
@D.person.NULL@@D.person.O@@R.im.IM@+Obv:@D.person.NULL@@D.person.O@@R.im.IM@%>a NOUN_ENDLEX ; ! Non-3rd possession, im-poss, obviative -a
@D.person.NULL@@D.person.O@@D.im.IM@+Obv:@D.person.NULL@@D.person.O@@D.im.IM@%>a NOUN_ENDLEX ; ! Non-3rd possession, 0-poss, obviative -a
@D.person.NULL@@R.person.O@+Obv:@D.person.NULL@@R.person.O@%>a NOUN_ENDLEX ; ! 3rd possession, 0/im-poss, obviative -a
! Plural
@D.number.SG@@D.person.O@+Pl:@D.number.SG@@D.person.O@%>ak NOUN_ENDLEX ; ! Non 3rd possessed (non-possessed), plural -ak
! Locative
@D.loc.NULL@@R.person.NULL@+Loc:@D.loc.NULL@@R.person.NULL@%>i2hk NOUN_ENDLEX ;
@R.person.NULL@+Distr:@R.person.NULL@%>i2nâhk NOUN_ENDLEX ;

LEXICON NA_NUMBER_SUFFIXES_SG
@D.sg@+Sg:@D.sg@0 NOUN_ENDLEX ; ! No singular suffix required (may have been cancelled due possessive theme or diminutive morpheme)
@R.sg.A@+Sg:@R.sg.A@%>a NOUN_ENDLEX ; ! Singular suffix -a required
@R.sg.I@+Sg:@R.sg.I@%>i NOUN_ENDLEX ; ! Singular suffix -i required
!! Potentially obsolete code
! @R.person.NULL@@D.sg@+Sg:@R.person.NULL@@D.sg@0 NOUN_ENDLEX ; ! No possession, no singular suffix
! @R.person.NULL@@R.sg.A@+Sg:@R.person.NULL@@R.sg.A@%>a NOUN_ENDLEX ; ! No possession, single-syllable a-singular (possessive suffix -im ruled out earlier)
! @R.person.NULL@@R.sg.I@+Sg:@R.person.NULL@@R.sg.I@%>i NOUN_ENDLEX ; ! No possession, single-syllable i-singular (possessive suffix -im ruled out earlier)
! @D.person.NULL@@D.person.O@@R.im.IM@@D.err.Thm@@R.sg.A@+Sg:@D.person.NULL@@D.person.O@@R.im.IM@@D.err.Thm@@R.sg.A@0 NOUN_ENDLEX ; ! Non-3rd possession, correct im-poss changing single-syllable a-singular to 0-singular
! @D.person.NULL@@D.person.O@@R.im.IM@@D.err.Thm@@R.sg.I@+Sg:@D.person.NULL@@D.person.O@@R.im.IM@@D.err.Thm@@R.sg.I@0 NOUN_ENDLEX ; ! Non-3rd possession, correct im-poss changing single-syllable i-singular to 0-singular
! @D.person.NULL@@D.person.O@@R.im.IM@@D.err.Thm@@D.sg@+Sg:@D.person.NULL@@D.person.O@@R.im.IM@@D.err.Thm@@D.sg@0 NOUN_ENDLEX ; ! Non-3rd possession, correct im-poss, multisyllabic 0-singular
! @D.person.NULL@@D.person.O@@R.im.IM@@R.err.Thm@@D.sg@+Sg:@D.person.NULL@@D.person.O@@R.im.IM@@R.err.Thm@@D.sg@0 NOUN_ENDLEX ; ! Non-3rd possession, correct im-poss, multisyllabic 0-singular
! @D.person.NULL@@D.person.O@@D.im.IM@@D.sg@+Sg:@D.person.NULL@@D.person.O@@D.im.IM@@D.sg@0 NOUN_ENDLEX ; ! Non-3rd possession, correct 0-poss, multisyllabic 0-singular
! @D.person.NULL@@D.person.O@@D.im.IM@@R.sg.A@+Sg:@D.person.NULL@@D.person.O@@D.im.IM@@R.sg.A@%>a NOUN_ENDLEX ; ! Non-3rd possession, correct 0-poss, single-syllabic a-singular
! @D.person.NULL@@D.person.O@@R.im.IM@@R.err.Thm@@R.sg.A@+Sg:@D.person.NULL@@D.person.O@@R.im.IM@@R.err.Thm@@R.sg.A@%>a NOUN_ENDLEX ; ! Non-3rd possession, incorrect 0-poss, single-syllabic a-singular
! @D.person.NULL@@D.person.O@@R.im.IM@@R.err.Thm@@R.sg.I@+Sg:@D.person.NULL@@D.person.O@@R.im.IM@@R.err.Thm@@R.sg.I@%>i NOUN_ENDLEX ; ! Non-3rd possession, incorrect 0-poss, single-syllabic i-singular

!! Suffixation for inanimate nouns

LEXICON NI_POSS_GATE1
@D.person.NULL@ NI_THEME_SUFFIX1 ;
@R.person.NULL@ NI_DIM_GATE ;

LEXICON NI_THEME_SUFFIX1
! Standard forms
@D.theme.IM@0                                                    NI_DIM_GATE ;
@R.theme.IM@@C.sg@:@R.theme.IM@@C.sg@%>i2m                       NI_DIM_GATE ;
@R.theme.IM2@@C.sg@:@R.theme.IM2@@C.sg@%>i2m                     NI_POSS_SUFFIXES ;
! Non-standard forms
@D.theme.IM@@P.err.Thm@@C.sg@:@D.theme.IM@@P.err.Thm@@C.sg@%>i2m NI_DIM_GATE ;
@R.theme.IM@@P.err.Thm@0                                         NI_DIM_GATE ;
@R.theme.IM2@@P.err.Thm@0                                        NI_POSS_SUFFIXES ;

LEXICON NI_DIM_GATE
@P.dim.NULL@                          NI_POSS_GATE2 ;
@D.dep@+Der/Dim+N+I:@D.dep@           NI_DIM_SUFFIX ;
@R.dep.DEP@+Der/Dim+N+I+D:@R.dep.DEP@ NI_DIM_SUFFIX ;

LEXICON NI_DIM_SUFFIX
! Standard forms
@R.dim.IS@@C.sg@:@R.dim.IS@@C.sg@%>i2^DIMs                               NI_POSS_GATE2 ;
@R.dim.ISIS@@C.sg@:@R.dim.ISIS@@C.sg@%>i2^DIMsîs                         NI_POSS_GATE2 ;
@D.dim@@C.sg@:@D.dim@@C.sg@%>i2^DIMs                                     NI_POSS_GATE2 ;
@D.dim@@C.sg@:@D.dim@@C.sg@%>i2^DIMsîs                                   NI_POSS_GATE2 ;
! Non-standard forms
@R.dim.DIM@@P.err2.Dim@@C.sg@:@R.dim.DIM@@P.err2.Dim@@C.sg@%>i2^DIMs     NI_POSS_GATE2 ;
@R.dim.DIM@@P.err2.Dim@@C.sg@:@R.dim.DIM@@P.err2.Dim@@C.sg@%>i2^DIMsîs   NI_POSS_GATE2 ;
@R.dim.ISIS@@P.err2.Dim@@C.sg@:@R.dim.ISIS@@P.err2.Dim@@C.sg@%>i2^DIMs   NI_POSS_GATE2 ;
@R.dim.IS@@P.err2.Dim@@C.sg@:@R.dim.IS@@P.err2.Dim@@C.sg@%>i2^DIMsîs     NI_POSS_GATE2 ;

LEXICON NI_POSS_GATE2
@D.person.NULL@ NI_THEME_SUFFIX2 ;
@R.person.NULL@ NI_NUMBER_LOC_SUFFIXES ;

LEXICON NI_THEME_SUFFIX2
! Standard forms
@D.theme.IM2@0                                                                             NI_POSS_SUFFIXES ;
@D.dim.NULL@@R.theme.IM2@@C.sg@:@D.dim.NULL@@R.theme.IM2@@C.sg@%>i2m                       NI_POSS_SUFFIXES ;
! Non-standard forms
@D.dim.NULL@@R.theme.IM2@@P.err.Thm@0                                                      NI_POSS_SUFFIXES ;
@D.dim.NULL@@D.theme.IM2@@P.err.Thm@@C.sg@:@D.dim.NULL@@D.theme.IM2@@P.err.Thm@@C.sg@%>i2m NI_POSS_SUFFIXES ;

! LEXICON NI_POSS_IM_0_SUFFIX
! @D.person.NULL@@R.im.IM@@C.sg@:@D.person.NULL@@R.im.IM@@C.sg@%>i2m                       NI_DIM_SUFFIXES ;
! @D.person.NULL@@R.im.IM@@P.err.Thm@:@D.person.NULL@@R.im.IM@@P.err.Thm@0                 NI_DIM_SUFFIXES ; ! Incorrectly using 0-poss rather than im-poss
! @D.person.NULL@@D.im.IM@                                                                 NI_DIM_SUFFIXES ;
! @D.person.NULL@@D.im.IM@@C.sg@@P.err.Thm@:@D.person.NULL@@D.im.IM@@C.sg@@P.err.Thm@%>i2m NI_DIM_SUFFIXES ; ! Incorrectly using im-poss rather than 0-poss
! @R.person.NULL@                                                              NI_DIM_SUFFIXES ;
! 
! LEXICON NI_DIM_SUFFIXES
! NI_POSS_SUFFIXES ;
! Independent NIs
! @D.dep.DEP@@D.dim@@D.isis.ISIS@@C.sg@+Der/Dim+N+I:@D.dep.DEP@@D.dim@@D.isis.ISIS@@C.sg@%>i2%^DIMs                         NI_POSS_SUFFIXES ;
! @D.dep.DEP@@D.dim@@D.is.IS@@C.sg@+Der/Dim+N+I:@D.dep.DEP@@D.dim@@D.is.IS@@C.sg@%>i2%^DIMsîs                               NI_POSS_SUFFIXES ;
! @D.dep.DEP@@D.dim@@R.isis.ISIS@@P.err2.Dim@@C.sg@+Der/Dim+N+I:@D.dep.DEP@@D.dim@@R.isis.ISIS@@P.err2.Dim@@C.sg@%>i2%^DIMs NI_POSS_SUFFIXES ;
! @D.dep.DEP@@D.dim@@R.is.IS@@P.err2.Dim@@C.sg@+Der/Dim+N+I:@D.dep.DEP@@D.dim@@R.is.IS@@P.err2.Dim@@C.sg@%>i2%^DIMsîs       NI_POSS_SUFFIXES ;
! Dependent NIs
! @R.dep.DEP@@D.dim@@D.isis.ISIS@@C.sg@+Der/Dim+N+I+D:@R.dep.DEP@@D.dim@@D.isis.ISIS@@C.sg@%>i2%^DIMs                         NI_POSS_SUFFIXES ;
! @R.dep.DEP@@D.dim@@D.is.IS@@C.sg@+Der/Dim+N+I+D:@R.dep.DEP@@D.dim@@D.is.IS@@C.sg@%>i2%^DIMsîs                               NI_POSS_SUFFIXES ;
! @R.dep.DEP@@D.dim@@R.isis.ISIS@@P.err2.Dim@@C.sg@+Der/Dim+N+I+D:@R.dep.DEP@@D.dim@@R.isis.ISIS@@P.err2.Dim@@C.sg@%>i2%^DIMs NI_POSS_SUFFIXES ;
! @R.dep.DEP@@D.dim@@R.is.IS@@@P.err2.Dim@C.sg@+Der/Dim+N+I+D:@R.dep.DEP@@D.dim@@R.is.IS@@P.err2.Dim@@C.sg@%>i2%^DIMsîs       NI_POSS_SUFFIXES ;

LEXICON NI_POSS_SUFFIXES
! Singular possessors
@U.person.NI@+Px1Sg:@U.person.NI@0 NI_NUMBER_LOC_SUFFIXES ;
@U.person.KI@+Px2Sg:@U.person.KI@0 NI_NUMBER_LOC_SUFFIXES ;
@U.person.O@+Px3Sg:@U.person.O@0   NI_NUMBER_LOC_SUFFIXES ;
@U.person.NULL@                    NI_NUMBER_LOC_SUFFIXES ;
! Plural possessors
@U.person.NI@@C.sg@+Px1Pl:@U.person.NI@@C.sg@%>i2nân  NI_NUMBER_LOC_SUFFIXES ;
@U.person.KI@@C.sg@+Px12Pl:@U.person.KI@@C.sg@%>i2naw NI_NUMBER_LOC_SUFFIXES ;
@U.person.KI@@C.sg@+Px2Pl:@U.person.KI@@C.sg@%>i2wâw  NI_NUMBER_LOC_SUFFIXES ;
@U.person.O@@C.sg@+Px3Pl:@U.person.O@@C.sg@0%>i2wâw   NI_NUMBER_LOC_SUFFIXES ;
! Obviative possessor
@U.person.O@@C.sg@@C.sg@+Px4Sg/Pl:@U.person.O@@C.sg@@C.sg@%>i2thiw NI_NUMBER_LOC_SUFFIXES ;
! Unspecified possessor
@U.person.MI@+PxX:@U.person.MI@0        NI_NUMBER_LOC_SUFFIXES ;
NI_POSS_LOC_COMBO_SUFFIXES ;

LEXICON NI_POSS_LOC_COMBO_SUFFIXES
! Singular possessors
@U.person.NI@+Px1Sg+Loc:@U.person.NI@%>i2hk  NOUN_ENDLEX ;
@U.person.NI@+Px1Sg+Loc:@U.person.NI@%>i2h  NOUN_ENDLEX ;
@U.person.KI@+Px2Sg+Loc:@U.person.KI@%>i2hk NOUN_ENDLEX ;
@U.person.KI@+Px2Sg+Loc:@U.person.KI@%>i2h NOUN_ENDLEX ;
@U.person.O@+Px3Sg+Loc:@U.person.O@%>i2hk   NOUN_ENDLEX ;
@U.person.O@+Px3Sg+Loc:@U.person.O@%>i2h   NOUN_ENDLEX ;
! @U.person.NULL@                    NI_NUMBER_LOC_SUFFIXES ;
! Plural possessors
@U.person.NI@+Px1Pl+Loc:@U.person.NI@%>i2nâhk  NOUN_ENDLEX ;
@U.person.NI@+Px1Pl+Loc:@U.person.NI@%>i2nâh  NOUN_ENDLEX ;
@U.person.KI@+Px12Pl+Loc:@U.person.KI@%>i2nâhk NOUN_ENDLEX ;
@U.person.KI@+Px12Pl+Loc:@U.person.KI@%>i2nâh NOUN_ENDLEX ;
@U.person.KI@+Px2Pl+Loc:@U.person.KI@%>i2wâhk  NOUN_ENDLEX ;
@U.person.KI@+Px2Pl+Loc:@U.person.KI@%>i2wâh  NOUN_ENDLEX ;
@U.person.O@+Px3Pl+Loc:@U.person.O@0%>i2wâhk   NOUN_ENDLEX ;
@U.person.O@+Px3Pl+Loc:@U.person.O@0%>i2wâh   NOUN_ENDLEX ;
! Obviative possessor
@U.person.O@+Px4Sg/Pl+Loc:@U.person.O@%>i2thihk NOUN_ENDLEX ;
@U.person.O@+Px4Sg/Pl+Loc:@U.person.O@%>i2thih NOUN_ENDLEX ;
! Unspecified possessor
@U.person.MI@+PxX+Loc:@U.person.MI@%>i2hk NOUN_ENDLEX ;
@U.person.MI@+PxX+Loc:@U.person.MI@%>i2h NOUN_ENDLEX ;

LEXICON NI_NUMBER_LOC_SUFFIXES
@D.number.PL@ NI_NUMBER_SUFFIXES_SG ; 
@D.number.SG@+Pl:@D.number.SG@%>a NOUN_ENDLEX ;
@R.person.NULL@+Loc:@R.person.NULL@%>i2hk NOUN_ENDLEX ;
@R.person.NULL@+Loc:@R.person.NULL@%>i2h NOUN_ENDLEX ;
! @R.person.NULL@+Loc+Dial/East:@R.person.NULL@%>i2h NOUN_ENDLEX ;

LEXICON NI_NUMBER_SUFFIXES_SG
@D.sg@+Sg:@D.sg@0 NOUN_ENDLEX ; ! Zero Sg morpheme for non NI-4w cases
@R.sg.I@+Sg:@R.sg.I@%>i NOUN_ENDLEX ; ! -i singular (NI-4w), when no other preceding morphology (-im, -is(is))
!! Likely obsolete code
! @D.sg@+Sg:@D.sg@0 NOUN_ENDLEX ; ! Zero Sg morpheme for non NI-4w cases
! @R.person.NULL@@R.sg.I@+Sg:@R.person.NULL@@R.sg.I@%>i NOUN_ENDLEX ; ! -i-singular (NI-4w), when no other morphology
! @D.person.NULL@@R.im.IM@@R.err.Thm@@R.sg.I@+Sg:@D.person.NULL@@R.im.IM@@R.err.Thm@@R.sg.I@%>i    NOUN_ENDLEX ; ! i-singular (NI-4w), when -im possessive theme required but missing
! @D.person.NULL@@R.im.IM@@D.err.Thm@@R.sg.I@+Sg:@D.person.NULL@@R.im.IM@@D.err.Thm@@R.sg.I@0    NOUN_ENDLEX ; ! 0-singular (NI-4w), when -im possessive theme required and found 
! @D.person.NULL@@D.im.IM@@R.err.Thm@@R.poss.PL@@R.sg.I@+Sg:@D.person.NULL@@D.im.IM@@R.err.Thm@@R.poss.PL@@R.sg.I@%>i NOUN_ENDLEX ; ! i-singular (NI-4w), when plural possession and incorrect 0 possessive theme
! @D.person.NULL@@D.im.IM@@D.err.Thm@@R.poss.PL@@R.sg.I@+Sg:@D.person.NULL@@D.im.IM@@D.err.Thm@@R.poss.PL@@R.sg.I@0 NOUN_ENDLEX ; ! 0-singular (NI-4w), when plural possession and incorrect -im possessive theme
! @D.person.NULL@@D.im.IM@@R.err.Thm@@D.poss.PL@@R.sg.I@+Sg:@D.person.NULL@@D.im.IM@@R.err.Thm@@D.poss.PL@@R.sg.I@0 NOUN_ENDLEX ; ! 0-singular (NI-4w), when singular possession and incorrect -im possessive theme
! @D.person.NULL@@D.im.IM@@D.err.Thm@@D.poss.PL@@R.sg.I@+Sg:@D.person.NULL@@D.im.IM@@D.err.Thm@@D.poss.PL@@R.sg.I@%>i NOUN_ENDLEX ; ! -i-singular (NI-4w), when singular possession and correct 0 possessive theme

!! Irregular nouns

LEXICON NOUN_IRREGULARS
! Irregular forms of ôsi
ôsi+N+I+Px1Sg:nitôt OSI_SUFFIXES ;
ôsi+N+I+Px2Sg:kitôt OSI_SUFFIXES ;
ôsi+N+I+Px3Sg:otôt OSI_SUFFIXES ;
ôsi+N+I+Px1Pl:nitôtinân OSI_SUFFIXES ;
ôsi+N+I+Px12Pl:kitôtinaw OSI_SUFFIXES ;
ôsi+N+I+Px2Pl:kitôtiwâw OSI_SUFFIXES ;
ôsi+N+I+Px3Pl:otôtiwâw OSI_SUFFIXES ;
ôsi+N+I+Px4Sg/Pl:otôtithiw OSI_SUFFIXES ;
! Locative combos
ôsi+N+I+Px1Sg+Loc:nitôtihk # ;
ôsi+N+I+Px1Sg+Loc:nitôtih # ;
ôsi+N+I+Px2Sg+Loc:kitôtihk # ;
ôsi+N+I+Px2Sg+Loc:kitôtih # ;
ôsi+N+I+Px3Sg+Loc:otôtihk # ;
ôsi+N+I+Px3Sg+Loc:otôtih # ;
ôsi+N+I+Px1Pl+Loc:nitôtinâhk # ;
ôsi+N+I+Px1Pl+Loc:nitôtinâh # ;
ôsi+N+I+Px12Pl+Loc:kitôtinâhk # ;
ôsi+N+I+Px12Pl+Loc:kitôtinâh # ;
ôsi+N+I+Px2Pl+Loc:kitôtiwâhk # ;
ôsi+N+I+Px2Pl+Loc:kitôtiwâh # ;
ôsi+N+I+Px3Pl+Loc:otôtiwâhk # ;
ôsi+N+I+Px3Pl+Loc:otôtiwâh # ;
ôsi+N+I+Px4Sg/Pl+Loc:otôtithihk # ;
ôsi+N+I+Px4Sg/Pl+Loc:otôtithih # ;

LEXICON OSI_SUFFIXES
+Sg:0 # ;
+Pl:a # ;


!! NOUN_ENDLEXs for wrapping up various things

LEXICON NOUN_ENDLEX
! Adding diminutive trigger to lexicalized diminutives to palatalize potential poss. person prefixes
@R.dim.DIM@:@R.dim.DIM@%^DIM NOUN_ENDLEX2 ;
@D.dim.DIM@ NOUN_ENDLEX2 ;

LEXICON NOUN_ENDLEX2
@R.err.Thm@+Err/Thm:@R.err.Thm@0 NOUN_ENDLEX3 ;
@D.err.Thm@ NOUN_ENDLEX3 ;

LEXICON NOUN_ENDLEX3
@R.err2.Dim@+Err/Dim:@R.err2.Dim@0 # ;
@D.err2.Dim@ # ;

!! End of noun affixes LEXC code
