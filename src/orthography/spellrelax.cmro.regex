# Copyright © 2015–2019 Alberta Language Technology Lab (ALTLab)
# http://altlab.artsrn.ualberta.ca/
#
# This program is free software; you can redistribute and/or modify
# this file under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program. If not, see <https://www.gnu.org/licenses/>.

# Plains Cree long vowel variation.

# short-i elision
i (->) [ %' | ‘ | ’ ],

a (->) {ā}, # a + combining macron U+0304
e (->) {ī}, # i + combining macron U+0304
{oo} (->) {ō}, # o + combining macron U+0304

a (->) ā, # a macron
e (->) ī, # i macron
{oo} (->) ō, # o macron

a (->) {â}, # a + combining circumflex accent U+0302
e (->) {î}, # i + combining circumflex accent U+0302
{oo} (->) {ô}, # o + combining circumflex accent U+0302

# Long vowel markings are frequently dropped:

a (->) u,
e (->) i,
{oo} (->) o,

# NS 152 materials consistantly write some vowels as long where Arok's materials write
# them as short.
# E.G. NS 152 give 'askîy' and Arok gives 'askiy.'
u (->) a,
i (->) e,
o (->) {oo} ,,

# Prefix-final vowel (preceded by consonant) dropped before initial vowel for following morpheme
[ a | e | i | o | u ]  (->) 0 || [ c h | k | m | n | p | s | t | w | y ] _ %- [ a | e | i | o | u ] ,,

# Dealing with spaces instead of hyphens as separators in multipart words

[ %- ] (->) [ %- ]+,  # Multiple hyphens into a single hyphen
! [ "@U.joiner.NULL@" %- ] (->) 0, # Missing hyphens between prefixes/preverbs
%- (->) [ %  | %  | %  ]+, # Multiple non-breaking or standard spaces SPACE + NBSP + NNBSP (in syllabics) to single hyphen

# Dealing with English-influenced divergences from SRO, and misspellings based on modern pronunciation
# Consonants
k (->) g,   # intervocalic voiced pronunciation to stops, maybe context should be added (including <c> overgenerates)
p (->) b,   # intervocalic voiced pronunciation to stops, maybe context should be added
t (->) d,   # intervocalic voiced pronunciation to stops, maybe context should be added
c (->) j,   # English influence on spelling <c>

# Glides
# e.g., "tanisi ê-isiyihkâsoyan -> "tan'si ê-sikâsowan"
# TODO: figure out what conditions this (probably /o/ or /i/)
# These rules without context are EXCESSIVELY productive!
# y (->) w,
# w (->) y,

{iw} (->) {ow},     # Misspelling based on assimilated pronunciation of underlying /i/ as /o/ before /w/
{ow} (->) {iw},
{iyi} (->) [ î | i | e ],       # Mishearing of vowel-glide-vowel cluster
{nwa} (->) {no},,    # <nwa> is likely to be heard as <no>

[..] (->) h || _ .#. ,, # Excessive word-final pre-aspiration
   h (->) 0 || [ a | e | i | o | u ] _ [ c h | k | p | t ] ,, # Missing preaspiration before stops
[..] (->) h || [ a | e | i | o | u ] _ [ c h | k | p | t ] ,, # Excessive preaspiration before stops

# Adding missing or reduced short i's in interconsonantal contexts
i (->) [..] || [ c h | h | k | m | n | p | s | t | w | y ] _ [ c h | h | k | m | n | p | s | t | w | y ] ,,

{ch} (->) {tch},          # English influence on spelling <c>
{ch} (->) {ts} ,,         # Another English influence on spelling <c>
{ch} (->) {c} || _ \h ,,  # English influence on spelling <c>

# Standardizing SRO to CMRO and double-vowel notation for long vowels
a (->) [ â | {aa} ],
u (->) a,
e (->) {ii},
{oo} (->) ô ,,

# Plains/Proto-Cree to Woods Cree
{th} (->) [ ý | y ] ,,
e (->) [ ê | î | {ee} ]

;

# Explanation:
# lexical side (->) input
# Or, in other words:
# correct (->) in use out there
